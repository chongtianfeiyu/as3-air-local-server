<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   creationComplete="creationCompleteHandler(event)"
			   applicationDPI="160">
	<fx:Script>
		<![CDATA[
			import com.adobe.images.JPGEncoder;
			
			import mx.events.FlexEvent;
			
			private var socket:Socket;
			private var video:Video;
			private var camera:Camera;
			private var jpgEncode:JPGEncoder;
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				jpgEncode = new JPGEncoder(100);
			}
			
			protected function sendSnapshotHandler(event:MouseEvent):void
			{
				var bmpData:BitmapData = new BitmapData(video.width, video.height, false);
				bmpData.draw(video);
				var jpgBytes:ByteArray = jpgEncode.encode(bmpData);
				var o:Object = {command: "IMAGE", data: jpgBytes};
				socket.writeObject(o);
				socket.flush();
			}
			
			protected function connect():void
			{
				if(socket != null)
				{
					//cleanup
				}
				socket = new Socket();
				
				socket.addEventListener(Event.CONNECT, connectHandler);
				socket.addEventListener(Event.CLOSE, closeHandler);
				
				socket.connect(ip.text, int(port.text));
			}
			
			private function connectHandler(event:Event):void
			{
				currentState = "connected";
				if(video == null)
				{
					video = new Video(320, 240);
					video.rotation = 90;
					video.x = 240;
					container.addChild(video);
					camera = Camera.getCamera();
					camera.setMode(320, 240, 25);
					video.attachCamera(camera);
				}
			}
			
			private function closeHandler(event:Event):void
			{
				currentState = "not_connected";
			}
			
		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="not_connected"/>
		<s:State name="connected"/>
	</s:states>
	<s:layout>
		<s:VerticalLayout paddingLeft="10" paddingRight="10" paddingTop="10" paddingBottom="10"/>
	</s:layout>
	
	<s:VGroup width="100%" height="100%" includeIn="not_connected">
		<s:Label text="IP:" />
		<s:TextInput id="ip" text="192.168.1.7"/>
		<s:Label text="Port:" />
		<s:TextInput id="port" text="1234"/>
		<s:Button label="Connect" click="connect()"/>
	</s:VGroup>
	<s:Label text="Image Client" includeIn="connected" fontSize="22" fontWeight="bold"/>
	<s:VGroup width="100%" includeIn="connected">
		<s:SpriteVisualElement id="container" width="240" height="320">
		</s:SpriteVisualElement>
		<s:Button label="Send Snapshot" click="sendSnapshotHandler(event)" />
	</s:VGroup>
</s:Application>
